plugins {
    id "com.android.application"
    id "com.google.protobuf" version "0.9.4"
    id "org.jetbrains.kotlin.android"
}

android {
    namespace "com.beackers.regenwall"
    compileSdk 34
    
    defaultConfig {
        applicationId "com.beackers.regenwall"
        minSdk 29
        targetSdk 31
        versionCode 6
        versionName "1.0"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    signingConfigs {
        release {
            storeFile file("${rootProject.projectDir}/regenwall.keystore")
            storePassword System.getenv("RELEASE_KEYSTORE_PASSWORD")
            keyAlias System.getenv("RELEASE_KEY_ALIAS")
            keyPassword System.getenv("RELEASE_KEY_PASSWORD")
        }
        debug {
            storeFile file("${rootProject.projectDir}/regenwall.keystore")
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyAlias System.getenv("KEY_ALIAS")
            keyPassword System.getenv("KEY_PASSWORD")
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile(
                "proguard-android-optimize.txt"
            ), "proguard-rules.pro"
        }
        debug {
            debuggable true
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            signingConfig signingConfigs.debug
        }
    }
    lint {
        abortOnError false
        checkReleaseBuilds false
        textReport true
        htmlReport true
        xmlReport true

        htmlOutput file("$buildDir/reports/lint/lint-results.html")
        xmlOutput file("$buildDir/reports/lint/lint-results.xml")
        textOutput file("$buildDir/reports/lint/lint-results.txt")
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.25.1"
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option "lite"
                }
            }
        }
    }
}

dependencies {
    implementation "androidx.appcompat:appcompat:1.6.1"
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    implementation "androidx.core:core:1.12.0"
    implementation(platform("org.jetbrains.kotlin:kotlin-bom:1.8.22"))
    implementation "androidx.datastore:datastore:1.2.0"
    implementation "com.google.protobuf:protobuf-javalite:3.25.1"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3"
    implementation "androidx.recyclerview:recyclerview:1.3.2"
}
